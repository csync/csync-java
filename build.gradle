plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

def mavenVersion='1.4.1'
group 'com.ibm'
version "${mavenVersion}"

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'

mainClassName = "Main"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}


compileJava {
    options.compilerArgs << "-Xlint:all" << "-Werror"
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.22'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.22'

    compile group: 'com.squareup.okhttp3', name: 'okhttp-ws', version: '3.4.2'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile group: 'com.h2database', name: 'h2', version: '1.4.193'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

publishing {
    repositories {
        maven{
            url "github.com/csync/csync-java"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
	    artifact sourcesJar
            groupId 'com.ibm.csync'
            artifactId 'csync-java'
            version "${mavenVersion}"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        version {
            name = "${mavenVersion}"
        }
        repo = 'maven'
        name = 'csync-java'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/csync/csync-java.git'
    }
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
